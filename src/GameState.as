package  
{
	import Box2DAS.Dynamics.ContactEvent;
	import com.citrusengine.core.State;
	import com.citrusengine.math.MathVector;
	import com.citrusengine.objects.platformer.Hero;
	import com.citrusengine.objects.platformer.Sensor;
	import com.citrusengine.physics.Box2D;
	import com.citrusengine.utils.ObjectMaker;
	import com.citrusengine.core.CitrusEngine;
	
	public class GameState extends State
	{
		private var _levelData:XML;
		private var _hero:Hero;
		private var _resetSensor:Sensor;
		
		public function GameState(levelData:XML) 
		{
			//This is the level XML file that was generated by the Level Architect.
			_levelData = levelData;
		}
		
		override public function initialize():void
		{
			super.initialize();
			
			//Create Box2D
			var box2D:Box2D = new Box2D("Box2D");
			add(box2D);
			
			box2D.visible = true;
			
			//Create the level objects from the XML file.
			if (_levelData)
				ObjectMaker.FromLevelArchitect(_levelData);
			
			//Find the hero object, and make it the camera target if it exists.
			_hero = getFirstObjectByType(Hero) as Hero;
			if (_hero)
			{
				view.cameraTarget = _hero;
				view.cameraOffset = new MathVector(stage.stageWidth / 2, stage.stageHeight / 2);
				view.cameraEasing = new MathVector(0.4, 0.4);
			}
			
			_resetSensor = getObjectByName("resetSensor") as Sensor;
			_resetSensor.onBeginContact.add(resetLevel);
		}
		
		private function resetLevel(e:ContactEvent):void {
			if (e.other.GetBody().GetUserData() is Hero) {
				CitrusEngine.getInstance().state = new GameState(_levelData);
			}
		}
	}

}